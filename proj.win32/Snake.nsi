SetCompressor lzma
Unicode true

; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Snake"
!define PRODUCT_VERSION "1.0"
!define PRODUCT_PUBLISHER "lxfly2000"
!define PRODUCT_WEB_SITE "https://github.com/lxfly2000/Snake"

; MUI 1.67 compatible ------
!include "MUI2.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "res\game.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Instfiles page
!insertmacro MUI_PAGE_INSTFILES

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "SimpChinese"

; MUI end ------

RequestExecutionLevel user
ManifestDPIAware true

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "Snake.exe"
InstallDir "$TEMP\${PRODUCT_NAME}"
ShowInstDetails show
ShowUnInstDetails show

VIProductVersion "1.0.0.1"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "ProductName" "${PRODUCT_NAME}"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "Comments" "${PRODUCT_NAME} 安装程序"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "CompanyName" "${PRODUCT_PUBLISHER}"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "LegalTrademarks" "${PRODUCT_NAME} 是 ${PRODUCT_PUBLISHER} 的合法商标。"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "LegalCopyright" "Copyright (C) 2019 ${PRODUCT_PUBLISHER}, 本程序发布网址：${PRODUCT_WEB_SITE}"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "FileDescription" "${PRODUCT_NAME} 安装程序"
VIAddVersionKey /LANG=${LANG_SIMPCHINESE} "FileVersion" "1.0.0.0"

Function CheckVCRedist
  Pop $0
  Pop $1
  Pop $2
  IfFileExists "$SYSDIR\$0" 0 +2
  Return
  NSISdl::download /TRANSLATE2 "正在下载 %s，请等待完成后选择“是”允许运行……" "连接中……" "（剩余1秒）" "（剩余1分钟）" "（剩余1小时）" "（剩余%u秒）" "（剩余%u分钟）" "（剩余%u小时）" "已下载 %s KB（%d%%），总计 %s KB，速度 %u.%01u KB/秒" /TIMEOUT=10000 $1 "$INSTDIR\$2"
  Pop $R0 ;Get the return value
  StrCmp $R0 "success" 0 +3
  ExecShellWait "open" "$INSTDIR\$2" "/passive /norestart"
  Return
  MessageBox MB_ICONEXCLAMATION "下载$\n$1$\n失败，请手动下载安装。$\n$\n如需复制链接请按 Ctrl+C."
FunctionEnd

!include "WinVer.nsh"

Function .onInit
  ;让我康康你运行的是不是XP啊？
  ${IfNot} ${AtLeastWinVista}
    MessageBox MB_ICONEXCLAMATION "当前运行的操作系统不支持运行此程序，$\n请更新至 Windows Vista 或以上版本后安装。$\n$\n支持的操作系统：$\nWindows Vista$\tWindows Server 2008$\nWindows 7$\tWindows Server 2008 R2$\nWindows 8$\tWindows Server 2012$\nWindows 8.1$\tWindows Server 2012 R2$\nWindows 10$\tWindows Server 2016"
    Abort
  ${EndIf}
FunctionEnd

Section "MainSection" SEC01
  SetOutPath "$INSTDIR"
  SetOverwrite ifnewer
  File "Release.win32\glew32.dll"
  File "Release.win32\libcocos2d.dll"
  File "Release.win32\libcrypto-1_1.dll"
  File "Release.win32\libcurl.dll"
  File "Release.win32\libmpg123.dll"
  File "Release.win32\libogg.dll"
  File "Release.win32\libssl-1_1.dll"
  File "Release.win32\libvorbis.dll"
  File "Release.win32\libvorbisfile.dll"
  File "Release.win32\OpenAL32.dll"
  File "Release.win32\sqlite3.dll"
  File "Release.win32\uv.dll"
  File "Release.win32\websockets.dll"
  File "Release.win32\zlib1.dll"
  File "Release.win32\Snake.exe"
  File /r "Release.win32\Resources"
  SetAutoClose true

  Push "vcredist_x86(10).exe"
  Push "http://download.microsoft.com/download/C/6/D/C6D0FD4E-9E53-4897-9B91-836EBA2AACD3/vcredist_x86.exe"
  Push "msvcr100.dll"
  Call CheckVCRedist
  Push "vcredist_x86(12).exe"
  Push "http://download.microsoft.com/download/9/C/D/9CD480DC-0301-41B0-AAAB-FE9AC1F60237/VSU4/vcredist_x86.exe"
  Push "msvcr110.dll"
  Call CheckVCRedist
  Push "vcredist_x86(13).exe"
  Push "http://download.microsoft.com/download/F/3/5/F3500770-8A08-488E-94B6-17A1E1DD526F/vcredist_x86.exe"
  Push "msvcr120.dll"
  Call CheckVCRedist
  Push "vcredist_x86(19).exe"
  Push "http://download.visualstudio.microsoft.com/download/pr/c8edbb87-c7ec-4500-a461-71e8912d25e9/99ba493d660597490cbb8b3211d2cae4/vc_redist.x86.exe"
  Push "vcruntime140.dll"
  Call CheckVCRedist
  Push "vcredist_x86(19).exe"
  Push "http://download.visualstudio.microsoft.com/download/pr/c8edbb87-c7ec-4500-a461-71e8912d25e9/99ba493d660597490cbb8b3211d2cae4/vc_redist.x86.exe"
  Push "msvcp140.dll"
  Call CheckVCRedist
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  Exec '"$INSTDIR\uninst.exe" /PreExec=Snake.exe'
SectionEnd


!include "FileFunc.nsh"

Function un.onInit
  ${GetParameters} $R0
  ${GetOptions} $R0 "/PreExec=" $R0
  IfErrors +2
  ExecWait "$INSTDIR\$R0"
FunctionEnd

Section Uninstall
  RMDir /r "$INSTDIR"

  SetAutoClose true
SectionEnd
